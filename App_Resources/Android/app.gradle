// https://stackoverflow.com/a/39040755/1868395
def packageJsonFile = file("./../../../package.json")
def packageJson = new groovy.json.JsonSlurper().parseText(packageJsonFile.text)
def version = packageJson.version
// https://blog.jayway.com/2015/03/11/automatic-versioncode-generation-in-android-gradle/
def (major, minor, patch) = version.split(/\./).collect{it.toInteger()}
def versionInt = (major * 1000000) + (minor * 10000) + (patch * 100)

android {
  defaultConfig {
    versionCode versionInt
    versionName "$version"

    minSdkVersion 17
    generatedDensities = []
  }

  // Enable building of unsigned release APK
  // https://github.com/NativeScript/nativescript-cli/issues/3444
  buildTypes {
    release {
      if (System.getenv("RELEASE_UNSIGNED") == "1") {
        signingConfig null
      }
    }
  }

  aaptOptions {
    additionalParameters "--no-version-vectors"
  }
}
